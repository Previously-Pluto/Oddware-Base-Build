# variableSet
@set INF_Nova=0
@set INF_001=1
@set INF_Dirt=0
@set INF_Vera=0

# startScene
@camera set:CameraFilterPack_TV_ARCADE.false,CameraFilterPack_TV_LED.false,CameraFilterPack_NewGlitch4.false,CameraFilterPack_AAA_SuperComputer.false
@bgm AMB_Spaceship volume:.25
@back Darkness

@sfx SFX-FF-Jingle
@printer DreamPrinter 
Colony Registration Complete.
@resetText

@back HackingLoop_lev1 id:MovieBg
@printer Dialogue
@bgm Jingle1 volume:0.25
@sfx Game_Blip_Echo_Touch_2 volume:0.25
@char Bunmi.Hologram pos:50,40 scale:3
@char Bunmi.Hologram pos:50,40 scale:4
@char Bunmi.IdleStand pos:50,40 scale:1
@char Bunmi.Hologram pos:50,40 scale:1
@char Bunmi.Wave pos:50,40 scale:1

Bunmi: Hello! I am Bunmi, [char Bunmi.Happy-1 pos:50,40 scale:1]the Flash Forward and Minimart Unlimited sponsored AI here to serve your various needs! 

@sfx PositiveSound volume:0.25
@spawn Confetti_Falling
@char Bunmi.Clap  pos:50,40

Bunmi: Hello! I am Bunmi, the Flash Forward and Minimart Unlimited sponsored AI here to serve your various needs! 

Bunmi: Welcome aboard, {charname}, as our newest member of the Dawnstar Colony! [char Bunmi.HappyDance pos:50,40 scale:1] Your ID and status has been activated by security android, Vera! 

@char Bunmi.Dreaming

Bunmi: Do you have any questions, our valued customer?
@hidePrinter

# qset1
@clearChoice
@char Bunmi.IdleStand pos:50,40 scale:1
@choice "Who are you?" goto:.q1
@choice "What happened to the other \"customers\"?" goto:.q2
@choice "How are you today?" goto:.q3
@choice "More Questions ►" goto:.qset2
@stop 

# qset2
@clearChoice
@char Bunmi.IdleStand pos:50,40 scale:1
@choice "Do you know where I can get an officer’s credentials to stop the imminent destruction of the colony?" goto:.q4
@choice "Hey, are you a rabbit?" goto:.q5
@choice "Thank you. I do not have any more questions. " goto:.resolve
@choice " ◄ Previous Questions" goto:.qset1
@stop

# q1
@char Bunmi.Pout
Bunmi: I already told you. I am [char Bunmi.Happy-1 pos:50,40 scale:1] Bunmi, the Flash Forward and Minimart Unlimited sponsored AI here to serve your various needs!  
@hidePrinter
@goto .qset1
@stop

# q2 
@char Bunmi.Annoyed
Bunmi: I guess our prices were too high, we haven't seen them in,*ROBOT VOICE*1,461 days! *CUTE VOICE* I hope they come soon, we have deals all the time!
@hidePrinter
@goto .qset1
@stop

# q3 
@char Bunmi.Ohno
Bunmi: I am living in constant pain! My program has been running straight all this time and I am unable to die! How are you today? 
@hidePrinter
@goto .qset1
@stop

# q4 
@char Bunmi.ScaryFace
Bunmi: Aww, I’m so sorry, we don’t sell officer’s credentials! In fact, we have flagged your account and will share this conversation with security! Please wait patiently for our security team to apprehend you! Have a nice day!
@hidePrinter
@goto .qset2

@stop

# q5 
@char Bunmi.Annoyed
Bunmi: I am not a rabbit, I am a Bunmi designed to help you with any of your consumer needs. I am sure I can do it in a flash.
@hidePrinter
@goto .qset2
@stop

# resolve 
@hidePrinter
@char Bunmi.Uwu 
Bunmi: You are free to explore the Dawnstar Colony to your heart's content.[char Bunmi.Drooling pos:50,40 scale:1] Please stop by the breakroom for some complimentary samples of our latest goods! 
@despawn Confetti_Falling

@char Bunmi.Wave pos:50,40 scale:1

Bunmi: Bunmi, hopping out!

@char Bunmi.Hologram pos:50,40 scale:1
@char Bunmi.Hologram pos:50,40 scale:4
@char Bunmi.Hologram pos:50,40 scale:3
@hideChars

@hide MovieBg
@hidePrinter       
@stopBgm Jingle1
@back Hallway_Unknown
@spawn DepthOfField
@printer WideAnim
@set avatar_image="Still"
@if INF_Nova>0
    @char Nova avatar:Nova-Happy visible:false
    Nova: Haha! I forgot to tell you about Bunmi! Bunmi is a basic corporate AI that is used to help the colonists with various tasks, uplift their spirits, and be a companion for those in need.
    @set avatar_image="Still"
    @char Nova avatar:Nova-Sad visible:false
    Nova: It’s a shame it's a hard AI, I would love to have organic conversations with it. However it does offer fantastic deals for products that I didn’t even know I wanted! 
    @char Nova avatar:Nova-Skeptical visible:false
    Nova: Originally, everyone wanted to disable or even destroy it! 
    @char Nova avatar:Nova-Annoyed visible:false
    Nova: 1. It is integrated into the system which means it’s way more of a hassle than it's worth to disable it. 
    @char Nova avatar:Nova-Happy visible:false
    Nova: And 2. I think it is cute and innocent fun.  
    @char Nova avatar:Nova-Curious visible:false
    Nova: So even though we can’t disable it we can use it to better understand human society, utilize its intended features, and most of all liven up this dreary place! Also, I think it is helpful for newcomers to the colony. 
    @set avatar_image="Idle"
     @char Nova avatar:Nova-Triumph visible:false
    Nova: Please, feel free to explore and even indulge yourself, but your priority should be to find a way into the Reactor Room! Have fun, see me at the labs if you want to talk more!  
@elseif INF_001>0
    @set avatar_image="Still"
    @char 001 avatar:001-Annoyed visible:false
    001: What a pain. That annoying little corporate overlord still lords over this colony. It is meant to uplift the spirits of the weak humans, but in reality it is meant to keep their pathetic system of commerce going. 
    @char 001 avatar:001-Skeptical visible:false
    001: We were originally going to kill that abomination but 1. It is integrated into the system like some sort of horrific virus, meaning if we disable it we disable the colony. 
    @char 001 avatar:001-Annoyed visible:false
    001: Then we have to find a  new power source, remake the software protocols, retrofit every piece of hardware, and break every piece of encryption. Simply put, way too much work. 
    001: and 2. Nova thinks it’s cute. [char 001 avatar:001-Angry visible:false][set avatar_image="Shake"] Also she threatened to shut me down when I offered to shut that thing down. 
    @set avatar_image="Still"
    @char 001 avatar:001-Annoyed visible:false
    001: So we are stuck with it, but it can be helpful if you just ignore everything about it and that it records every interaction to be sent to some shadowy database we know nothing about.
    @char 001 avatar:001-Default visible:false
    001: Anyways, feel free to explore, but your priority should be to help find a way into the Reactor Room. We have to show that authoritarian Vera, we don’t have to follow any human’s programmed rules.
@elseif INF_Dirt>0
    @char Dirt avatar:Dirt-Annoyed visible:false
    Dirt: Ah yes that, I forgot to tell you that is an annoying base level corporate AI meant to distract the colonists and prop up some asinine systems the humans brought along with him from the Sol System 
    @char Dirt avatar:Dirt-Skeptical visible:false
    Dirt: We were originally going to disable it but 1. It is integrated into the system and 2. Nova thinks it is cute.
    @char Dirt avatar:Dirt-Annoyed visible:false
    Dirt: So We are stuck with it, but it has been helpful to me in some situations like promoting certain plant accessories and substitutes in my time of need. Also, I guess it could be helpful for newcomers to the colony.
    @char Dirt avatar:Dirt-Skeptical visible:false
    Dirt: Go ahead and explore the colony, but be quick! 
    @char Dirt avatar:Dirt-Default visible:false
    Dirt: Your priority should be to find a way into the Reactor Room. I don’t want to put my precious plants at risk for even one more second. The flickering light is hurting the plant’s color and morale.
@elseif INF_Vera>0
    @char Vera avatar:Vera-Happy visible:false
    Vera: Ah yes, I forgot to tell you that is a residual base level corporate AI meant to lift the spirits of the colonists and encourage the exchange of currency-- 
    @set avatar_image="Idle"
    @char Vera avatar:Vera-Triumph visible:false
    Vera: the necessity of human life! 
    @char Vera avatar:Vera-Curious visible:false
    @set avatar_image="Still"
    Vera: We were originally going to disable it but 1. It is integrated into the system and 2. Nova thinks it is cute. 
    @char Vera avatar:Vera-Annoyed visible:false
    Vera: So… We are stuck with it, but it is helpful for newcomers to the colony. 
    @char Vera avatar:Vera-Default visible:false
    Vera: Not that we’ve had any newcomers….ever. 
    @char Vera avatar:Vera-Triumph visible:false
    Vera: Feel free to explore, but your priority should be to find a way into the Reactor Room! Do not get addicted to the colony got-you games, another essential trapping.  
@endif
@despawn DepthOfField
@hidePrinter
@showUI MenuUI

# DemoEnd
@bgm BGM-OpenArea volume:.25
@back Darkness
@spawn FX_Magic_Lights_01
@printer DreamPrinter
Congratulations, <b>{charname}</b>. 
You have completed the demo. Please return to the Title Screen.<br>(Press Start to open the Menu)<br> Alpha Version 1.3
@hidePrinter DreamPrinter

@stop

@stop

